buildscript {
    ext {
        springBootVersion = '1.3.5.RELEASE'
        springPlatformVersion = '2.0.5.RELEASE'
        dependencyManagementPlugin = '0.5.2.RELEASE'
        springCloudVersion = '1.1.0.RELEASE'
        swaggerUiVersion = '2.1.4'
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "http://repo.spring.io/release" }
        maven { url "https://plugins.gradle.org/m2/" }		
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("io.spring.gradle:dependency-management-plugin:${dependencyManagementPlugin}")
        classpath("gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.6.0")
        classpath('de.undercouch:gradle-download-task:2.1.0')
    }
}

apply plugin: "io.spring.dependency-management"

apply plugin: 'spring-boot'

apply plugin: 'eclipse'

apply plugin: 'maven'

apply plugin: 'java'

apply plugin: "com.palantir.docker"

apply plugin: 'de.undercouch.download'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
        mavenBom("org.springframework.cloud:spring-cloud-netflix:${springCloudVersion}")
        mavenBom("io.spring.platform:platform-bom:${springPlatformVersion}")
    }
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter")
    
    compile("org.springframework.boot:spring-boot-starter-actuator")
        
    compile("org.springframework.boot:spring-boot-starter-web") {
        exclude module: 'spring-boot-starter-tomcat'
    }
    
    compile("org.springframework.boot:spring-boot-starter-undertow")
    
    compile("org.springframework.cloud:spring-cloud-starter-eureka")
    
    compile('org.springframework.cloud:spring-cloud-starter-zuul')
}

import de.undercouch.gradle.tasks.download.Download

task downloadSwaggerZipFile(type: Download) {
    src "https://github.com/swagger-api/swagger-ui/archive/v${swaggerUiVersion}.zip"
    dest new File(buildDir, 'swagger.zip')
}

task unzipSwaggerZipFile(dependsOn: downloadSwaggerZipFile, type: Copy) {
    from zipTree(downloadSwaggerZipFile.dest)
    into buildDir
}

task installSwagger(dependsOn: unzipSwaggerZipFile, type: Copy) {
	from new File(buildDir, "swagger-ui-${swaggerUiVersion}/dist")
	into new File(buildDir, 'resources/main/public/apidocs/swagger');
}

tasks.processResources.dependsOn installSwagger

docker {
    name jar.baseName
    dockerfile 'src/main/docker/Dockerfile'
	files 'ssl/docker/server.p12'
    dependsOn tasks.jar, tasks.bootRepackage, tasks.build
}
